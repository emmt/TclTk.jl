module TclImageTests

using TclTk
using Test
using Colors
using Colors: FixedPointNumbers

function compute_significant_bits(::Type{T}) where {T<:AbstractFloat}
    isbitstype(T) || throw(ArgumentError("type `$T` is not a bits type"))
    nmax = 8*sizeof(T) - 1 # at least one bit for sign
    for nbits in 0:nmax
        try
            i = one(Int128) << nbits
            x = convert(T, i)
            x == i && x - one(x) == i - one(i) || return nbits - 1
        catch ex
            return nbits - 1
        end
    end
    error("cannot determine the number of bits in the mantissa of `$T`")
end

@testset "Tk Utilities" begin
    @test compute_significant_bits(Float16) == TclTk.Impl.significant_bits(Float16)
    @test compute_significant_bits(Float32) == TclTk.Impl.significant_bits(Float32)
    @test compute_significant_bits(Float64) == TclTk.Impl.significant_bits(Float64)
    @test typemax(Int128) == TclTk.Impl.max_exact_int(BigFloat)
end

@testset "Tk Images" begin
    interp = @inferred tk_start()
    props = sort!([:height, :interp, :inuse, :name, :size, :type, :width])

    # Bitmap image.
    xbm = @inferred TkBitmap(:file=>joinpath(@__DIR__, "rule.xbm"))

    # Image properties.
    @test sort!(collect(propertynames(xbm))) == props
    @test xbm.type === :bitmap
    @test xbm.interp === interp
    @test xbm.inuse === false
    @test xbm.name isa TclObj
    @test startswith(string(xbm.name), "image")
    @test xbm.width === 32
    @test xbm.height === 17
    @test xbm.size === (32, 17)
    @test_throws KeyError xbm.non_existing_property

    # Image keys.
    @test haskey(xbm, :data) === true
    @test haskey(xbm, :file) === true
    @test haskey(xbm, :foreground) === true
    @test haskey(xbm, :maskdata) === true
    @test haskey(xbm, :maskfile) === true
    @test haskey(xbm, :non_existing_option) === false
    path = convert(String, @inferred xbm[:file])
    @test isfile(path)
    @test endswith(path, "rule.xbm")

    # Change content.
    xbm[:file] = joinpath(@__DIR__, "letters.xbm")
    @test xbm.size === (47, 35)
    path = convert(String, @inferred xbm[:file])
    @test isfile(path)
    @test endswith(path, "letters.xbm")

    # Photo image.
    png = @inferred TkPhoto(interp, "m51", :file => joinpath(@__DIR__, "m51-tiny.png"))
    @test png isa TkPhoto

    # Image properties.
    @test sort!(collect(propertynames(png))) == props
    @test png.type === :photo
    @test png.interp === interp
    @test png.inuse === false
    @test png.name == "m51"
    @test png.width === 48
    @test png.height === 64
    @test png.size === (48, 64)
    @test_throws KeyError png.non_existing_property

    # Image keys.
    @test haskey(png, :non_existing_option) === false
    @test haskey(png, :file) === true
    path = convert(String, @inferred png[:file])
    @test isfile(path)
    @test endswith(path, "m51-tiny.png")

    # Abstract array API for photo images.
    @test @inferred(ndims(png)) === 2
    @test @inferred(ndims(typeof(png))) === 2
    @test @inferred(eltype(png)) === RGBA{FixedPointNumbers.N0f8}
    @test @inferred(eltype(typeof(png))) === RGBA{FixedPointNumbers.N0f8}
    @test @inferred(size(png)) === png.size
    @test @inferred(length(png)) === prod(png.size)

end

end # module
